plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.3.0'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'io.freefair.lombok' version '9.1.0'
}

description = 'Highly customizable Discord Bot'

group = 'com.dscalzi'
base.archivesName = 'AventiBot'
def classifier = 'dist'

wrapper {
    gradleVersion = '9.2.1'
    distributionType = Wrapper.DistributionType.BIN
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://maven.lavalink.dev/releases" }
    maven { url "https://maven.topi.wtf/releases" }
    maven { url "https://maven.lavalink.dev/snapshots" }
}

dependencies {

    // From JDA - Code safety
    compileOnly("com.google.code.findbugs:jsr305:3.0.2")
    compileOnly("org.jetbrains:annotations:26.0.2-1")

    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "info.debatty:java-string-similarity:2.0.0"

    implementation "net.dv8tion:JDA:${jdaVersion}"
    implementation "dev.arbjerg:lavaplayer:${lavaplayerVersion}"
    implementation "dev.lavalink.youtube:common:${lavaplayerYtSrcVersion}"
    implementation "com.github.topi314.lavasrc:lavasrc:${lavasrcVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    implementation("net.sf.trove4j:core:3.1.0") {
        because 'JDA implementation dependency is used in creating console messages.'
    }
    implementation("org.apache.commons:commons-collections4:4.5.0") {
        because 'JDA implementation dependency is used in RestFuture constructor.'
    }

    testImplementation(platform("org.junit:junit-bom:${junitVersion}"))

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

compileJava.options.encoding = 'UTF-8'

processResources {
    from 'LICENSE.txt'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'com.dscalzi.aventibot.LaunchWrapper'
}

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

javafx {
    version = '17'
    modules = ['javafx.controls', 'javafx.fxml']
}

jar {
    manifest {
        attributes 'Implementation-Version': archiveVersion.get(), 'Specification-Version': archiveVersion.get()
    }
}

shadowJar {
    archiveClassifier.set(classifier)
}
assemble.dependsOn('shadowJar')

String getDependencyVersion(name) {
    def res = ''

    configurations.compileClasspath.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        def id = artifact.moduleVersion.id
        if (id.name == name) {
            res = id.version
            return
        }
    }

    return res
}

tasks.register('updateReadme') {
    def readMe = new File(project.rootDir.toString() + '/README.md')

    def txt = readMe.text.replaceFirst(/JDA-(.+?)-9/, "JDA-${getDependencyVersion('JDA').replaceAll('_', '__').replaceAll('-', '--')}-9")
    readMe.text = txt.replaceFirst(/lavaplayer-(.*?)-/, "lavaplayer-${getDependencyVersion('lavaplayer')}-")
}

tasks.register('copyDeployment', Copy) {
    from layout.buildDirectory.file("libs/${base.archivesName.get()}-${version}-${classifier}.jar")
    into layout.projectDirectory.dir('deploy')
}
