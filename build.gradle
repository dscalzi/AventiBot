plugins {
    id 'java'
    id 'application'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'io.freefair.lombok' version '6.6.1'
}

mainClassName = 'com.dscalzi.aventibot.LaunchWrapper'

description = 'Highly customizable Discord Bot'

group = 'com.dscalzi'
archivesBaseName = 'AventiBot'

wrapper {
    gradleVersion = '7.6'
    distributionType = Wrapper.DistributionType.BIN
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://m2.dv8tion.net/releases' }
    maven { url 'https://jitpack.io' }
}

dependencies {

    // From JDA - Code safety
    compileOnly("com.google.code.findbugs:jsr305:3.0.2")
    compileOnly("org.jetbrains:annotations:23.0.0")

    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "info.debatty:java-string-similarity:2.0.0"

    implementation "net.dv8tion:JDA:${jdaVersion}"
//    implementation "com.sedmelluq:lavaplayer:${lavaplayerVersion}"
    implementation "com.github.walkyst:lavaplayer-fork:${lavaplayerVersion}"
    implementation "com.github.TopiSenpai.LavaSrc:lavasrc:${lavasrcVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    implementation("net.sf.trove4j:trove4j:3.0.3") {
        because 'JDA implementation dependency is used in creating console messages.'
    }
    implementation("org.apache.commons:commons-collections4:4.4") {
        because 'JDA implementation dependency is used in RestFuture constructor.'
    }

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

compileJava.options.encoding = 'UTF-8'

processResources {
    from 'LICENSE.txt'
}

license {
    header = rootProject.file('HEADER.txt')
    properties {
        name = 'Daniel D. Scalzi'
        year = '2016-2023'
        url = 'https://github.com/dscalzi/AventiBot'
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

javafx {
    version = "17"
    modules = ['javafx.controls', 'javafx.fxml']
}

jar {
    manifest {
        attributes 'Implementation-Version': archiveVersion.get(), 'Specification-Version': archiveVersion.get()
    }
}

shadowJar {
    archiveClassifier.set('dist')
}
assemble.dependsOn('shadowJar')

String getDependencyVersion(name) {
    def res = ''

    configurations.compileClasspath.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        def id = artifact.moduleVersion.id
        if (id.name == name) {
            res = id.version
            return
        }
    }

    return res
}

tasks.register('updateReadme') {
    def readMe = new File(project.rootDir.toString() + '/README.md')

    def txt = readMe.text.replaceFirst(/JDA-(.+?)-9/, "JDA-${getDependencyVersion('JDA').replaceAll('_', '__').replaceAll('-', '--')}-9")
    readMe.text = txt.replaceFirst(/lavaplayer-(.*?)-/, "lavaplayer-${getDependencyVersion('lavaplayer')}-")
}