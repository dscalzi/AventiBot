/*
 * AventiBot
 * Copyright (C) 2016-2018 Daniel D. Scalzi
 * See LICENSE.txt for license information.
 */

plugins {
    id'java'
    id'application'
    id'com.github.johnrengelman.shadow' version '2.0.1'
}

mainClassName = 'com.dscalzi.aventibot.LaunchWrapper'

def versionObj = new Version(major: 0, minor: 78, revision: 2)

description = """\
Highly customizable Discord Bot
------------------------------------------
Current Version: ${versionObj}
------------------------------------------
"""

group = 'com.dscalzi'
archivesBaseName = 'AventiBot'
version = "$versionObj"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile group: 'net.dv8tion', name: 'JDA', version:'3.5.1_339'
    compile group: 'com.sedmelluq', name:'lavaplayer', version:'1.2.53'
    compile group: 'com.google.code.gson', name: 'gson', version:'2.8.0'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

compileJava.options.encoding = 'UTF-8'

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version, 'Specification-Version': version
    }
}

shadowJar {
    classifier = "shaded"
}

task('updateReadme') {
	def readMe = new File(project.rootDir.toString() + '/README.md')
	
	def txt = readMe.text.replaceFirst(/JDA-(.+?)__(.+?)-/, 'JDA-' + getDepdencyVersion('JDA').replaceAll('_', '__') + '-')
	readMe.text = txt.replaceFirst(/lavaplayer-(.+?)-/, 'lavaplayer-' + getDepdencyVersion('lavaplayer') + '-')
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn shadowJar

    jar.mustRunAfter clean
    shadowJar.mustRunAfter jar
}

String getDepdencyVersion(name) {
	def res = ''

	configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
		def id = artifact.moduleVersion.id
		if(id.name.equals(name)) {
			res = id.version
			return;
		}
	}
	
	return res
}

class Version {
    String major, minor, revision

    String getBuild() {
        System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?: "DEV"
    }

    String toString() {
        "${major}.${minor}.${revision}_$build"
    }
}